# == Class: profile::apache_webserver
#
# Per the puppetlabs/apache documentation,
#
# "The Puppet module applies a default configuration based on your operating system; Debian, Red Hat, FreeBSD, and
# Gentoo systems each have unique default configurations. These defaults work in testing environments but are not
# suggested for production"
#
# So, this profile sets some hardened values that place apache into a sane and consistent state, ready for you to
# add apache::vhost resources to the node.
# See https://wiki.msi.umn.edu/display/AP/Puppetizing+Apache+Best+Practice.
#
# === Parameters
#
# Document parameters here.
#
class profile::apache_webserver {
  # define MSI-specific parameters and deploy incommon CA certs
  include apache_msi
  include apache_msi::httpoxy_mitigation

  class { 'apache':
    default_vhost          => false,
    default_confd_files    => true,
    default_mods           => true, # MSI not quite enough of a webhosting shop to handpick these, I think
    default_ssl_ca         => $apache_msi::params::incommon_ca_cert,
    default_ssl_chain      => $apache_msi::params::incommon_ca_chain,
    dev_packages           => '',
    error_documents        => false,

    # You'll want to override these in hiera node files, or at least review them.
    # wwwrun is a catch-all user, but there are real security benefits to running different webapps as different users.
    user                   => nobody,
    group                  => nobody,

    manage_user            => false,
    manage_group           => false,
    keepalive              => 'On',
    keepalive_timeout      => 15,
    max_keepalive_requests => 100,
    # mpm_module will default differently for Debian and RedHat; let's not be inconsistent.
    mpm_module             => 'prefork',
    # Yes, fully puppet-managed, please
    purge_configs          => true,
    serveradmin            => 'msi-asopsi@umn.edu',
    # Unsightly text on bottom of autogenerated pages
    server_signature       => 'Off',
    service_enable         => true,
    service_ensure         => 'running',
    service_manage         => true,
    # Nonya bidness
    trace_enable           => 'Off',
    root_directory_options => ['-Indexes', '+FollowSymLinks']
  }

  class { 'apache::mod::ssl':
    ssl_cipher             => $apache_msi::params::ssl_cipher
  }

}
